/-
Copyright 2026 The Beneficial AI Foundation. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Dablander
-/
import Curve25519Dalek.Funs
import Curve25519Dalek.Math.Ristretto.Representation
import Curve25519Dalek.Specs.Backend.Serial.U64.Field.FieldElement51.FromBytes
import Curve25519Dalek.Specs.Ristretto.RistrettoPoint.ElligatorRistrettoFlavor
import Curve25519Dalek.Specs.Ristretto.RistrettoPoint.Add

/-!
# Spec theorem for `curve25519_dalek::ristretto::RistrettoPoint::from_uniform_bytes`

The Ristretto group is a prime-order group constructed as a quotient of the Edwards curve.
`from_uniform_bytes` constructs a `RistrettoPoint` from 64 bytes of uniform random data with
a uniform distribution: if the input bytes are uniformly random, the output point is uniformly
distributed over the Ristretto group. The mapping is achieved by:

- Splitting the 64-byte input into two 32-byte halves
- Taking the low 255 bits of each half modulo p and converting to a field element (via `from_bytes`)
- Applying the Ristretto-flavored Elligator map to each field element to transform it into a
  Ristretto point
- Adding the two resulting Ristretto points via elliptic curve addition

Source: "curve25519-dalek/src/ristretto.rs"
-/

open Aeneas Aeneas.Std Result Aeneas.Std.WP
open curve25519_dalek.math
namespace curve25519_dalek.ristretto.RistrettoPoint

/-- Returns first 32 bytes of a 64-byte array -/
@[reducible]
def bytes_lower (bytes : Array U8 64#usize) : Array U8 32#usize :=
  ⟨(List.range 32).map (fun i => bytes[i]!), by simp⟩

/-- Returns last 32 bytes of a 64-byte array -/
@[reducible]
def bytes_upper (bytes : Array U8 64#usize) : Array U8 32#usize :=
  ⟨(List.range 32).map (fun i => bytes[32 + i]!), by simp⟩

/-- Maps 32 bytes to a mathematical field element: low 255 bits mod p -/
@[reducible]
def field_from_bytes (b : Array U8 32#usize) : ZMod p :=
  ((U8x32_as_Nat b % 2^255 : ℕ) : ZMod p)

/-- **Spec theorem for `curve25519_dalek::ristretto::RistrettoPoint::from_uniform_bytes`**
• The function always succeeds (no panic) for arbitrary 64-byte inputs.
• The result is a mathematically valid Ristretto point (i.e., an even Edwards point on the curve).
• The result equals `elligator(from_bytes(bytes[0..32])) + elligator(from_bytes(bytes[32..64]))` in
  the Ed25519 group, where `from_bytes` maps 32 bytes to a field element (low 255 bits mod p) and
  `elligator` is the Ristretto-flavored Elligator map.
-/
@[step]
theorem from_uniform_bytes_spec (bytes : Array U8 64#usize) :
    from_uniform_bytes bytes ⦃ (result : RistrettoPoint) =>
      result.IsValid ∧
      result.toPoint =
        (elligator_ristretto_flavor_pure (field_from_bytes (bytes_lower bytes))).val +
        (elligator_ristretto_flavor_pure (field_from_bytes (bytes_upper bytes))).val ⦄ := by
  unfold from_uniform_bytes
  unfold Insts.CoreOpsArithAddRistrettoPointRistrettoPoint.add
  step*
  · simp_all only [Array.val_to_slice,
      List.slice_zero_j, Slice.length, List.length_take,
      List.Vector.length_val, UScalar.ofNatCore_val_eq,
      Nat.reduceLeDiff, inf_of_le_left, tsub_zero,
      Array.repeat_val, List.reduceReplicate,
      List.length_cons, List.length_nil, zero_add,
      Nat.reduceAdd]
  · simp_all only [Array.val_to_slice,
      List.slice_zero_j, Slice.length, List.length_take,
      List.Vector.length_val, UScalar.ofNatCore_val_eq,
      Nat.reduceLeDiff, inf_of_le_left, tsub_zero,
      Array.repeat_val, List.reduceReplicate,
      List.length_cons, List.length_nil, zero_add,
      Nat.reduceAdd]
  · refine ⟨result_post1, ?_⟩
    rw [result_post2, R_1_post2, R_2_post2]
    simp only [backend.serial.u64.field.FieldElement51.toField]
    rw [(Montgomery.lift_mod_eq_iff _ _).mp r_1_post1,
      (Montgomery.lift_mod_eq_iff _ _).mp r_2_post1]
    simp_all only [Array.val_to_slice,
      List.slice_zero_j, Slice.length, List.length_take, List.Vector.length_val, tsub_zero]
    have hlen1 : s2.val.length = (32#usize).val := by
      have := congrArg List.length s1_post1
      rw [s2_post]
      simp only [List.length_take, List.Vector.length_val, UScalar.ofNatCore_val_eq] at this ⊢
      omega
    have hlen2 : s5.val.length = (32#usize).val := by
      have := congrArg List.length s4_post1
      rw [s5_post]
      simp only [List.slice_length, List.Vector.length_val, UScalar.ofNatCore_val_eq] at this ⊢
      omega
    have heq1 : (Array.repeat 32#usize 0#u8).from_slice s2 =
        (⟨(List.range 32).map (fun i => bytes[i]!), by simp⟩ : Array U8 32#usize) :=
      Subtype.ext (by
        rw [Array.from_slice_val _ _ hlen1, show s2.val = (↑s2 : List _) from rfl,
          s2_post, s1_post1]
        apply List.ext_getElem
        · simp only [List.length_take, List.length_map, List.length_range, List.Vector.length_val,
            UScalar.ofNatCore_val_eq]
          omega
        · intro i hi1 hi2
          simp only [List.getElem_map, List.getElem_range, Array.getElem!_Nat_eq, List.getElem_take]
          symm
          apply getElem!_pos)
    have heq2 : (Array.repeat 32#usize 0#u8).from_slice s5 =
        (⟨(List.range 32).map (fun i => bytes[32 + i]!), by simp⟩ : Array U8 32#usize) :=
      Subtype.ext (by
        rw [Array.from_slice_val _ _ hlen2, show s5.val = (↑s5 : List _) from rfl,
          s5_post, s4_post1]
        apply List.ext_getElem
        · simp only [List.slice_length, List.length_map, List.length_range, List.Vector.length_val,
            UScalar.ofNatCore_val_eq]
          omega
        · intro i hi1 hi2
          simp only [List.getElem_map, List.getElem_range, Array.getElem!_Nat_eq,
            List.slice, List.getElem_drop, List.getElem_take]
          symm
          apply getElem!_pos)
    rw [← s2_post, ← s5_post, heq1, heq2]

end curve25519_dalek.ristretto.RistrettoPoint
