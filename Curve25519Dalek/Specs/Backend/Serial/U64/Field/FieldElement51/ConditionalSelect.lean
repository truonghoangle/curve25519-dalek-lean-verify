/-
Copyright 2025 The Beneficial AI Foundation. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Hoang Le Truong
-/
import Curve25519Dalek.Funs

/-! # Spec theorem

Specification for
`curve25519_dalek::backend::serial::u64::field::FieldElement51::conditional_select`.

This function returns, limb-wise, either `a` or `b` depending on the constant-time `Choice`
flag. At the limb level it uses `u64`'s `ConditionallySelectable::conditional_select`, which
returns the first operand when `choice = 0` and the second operand when `choice = 1`.

Source: "curve25519-dalek/src/backend/serial/u64/field.rs#L228-L240"
-/

open Aeneas Aeneas.Std Result Aeneas.Std.WP
namespace curve25519_dalek.backend.serial.u64.field.FieldElement51.Insts
namespace SubtleConditionallySelectable

/-- **Spec theorem**

Specification for
`curve25519_dalek::backend::serial::u64::field::FieldElement51::conditional_select`.
• The function always succeeds (no panic)
• For each limb `i`, the result limb equals `b[i]` when `choice = 1`, and `a[i]` when
  `choice = 0` (constant-time conditional select)
• Consequently, when `choice = Choice.one`, the whole result equals `b`;
  when `choice = Choice.zero`, the result equals `a`.
-/
@[step]
theorem conditional_select_spec
    (a b : backend.serial.u64.field.FieldElement51)
    (choice : subtle.Choice) :
    conditional_select a b choice ⦃ (res : FieldElement51) =>
      ∀ i < 5,
        res[i]! = (if choice.val = 1#u8 then b[i]! else a[i]!) ⦄ := by
  unfold conditional_select
  unfold U64.Insts.SubtleConditionallySelectable.conditional_select
  by_cases h: choice.val = 1#u8
  · simp only [h, ite_true, bind_tc_ok]
    step*
    intro i hi
    subst_vars
    simp only [Array.getElem!_Nat_eq, Array.make, List.getElem!_eq_getElem?_getD]
    rcases i with _ | _ | _ | _ | _ | n <;> simp_all; omega
  · simp only [h, ite_false, bind_tc_ok]
    step*
    intro i hi
    subst_vars
    simp only [Array.getElem!_Nat_eq, Array.make, List.getElem!_eq_getElem?_getD]
    rcases i with _ | _ | _ | _ | _ | n <;> simp_all; omega

end SubtleConditionallySelectable
end curve25519_dalek.backend.serial.u64.field.FieldElement51.Insts
