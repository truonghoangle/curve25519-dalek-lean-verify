/-
Copyright 2025 The Beneficial AI Foundation. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Dablander, Oliver Butterley
-/
import Curve25519Dalek.Funs
import Curve25519Dalek.Math.Basic
import Curve25519Dalek.Aux
import Curve25519Dalek.Specs.Backend.Serial.U64.Constants.L

/-! # Spec theorem for `curve25519_dalek::backend::serial::u64::scalar::Scalar52::conditional_add_l`

This function conditionally adds the group order `L` to a `Scalar52` `self` based on a `Choice`
`condition`, returning the carry-out and the updated scalar. Concretely, for each limb `i` it
performs `carry' = (carry >> 52) + self[i] + addend` with `addend = L[i]` if `condition = 1`
or `0` otherwise, masks `self[i]` to the low 52 bits, and propagates the carry. The 52-bit
limb bound on the input keeps the intermediate `carry` below `2^53`, ruling out U64 overflow.

After the 5 limbs telescope, `Scalar52_as_Nat result + 2^260 * (final_carry / 2^52) =
Scalar52_as_Nat self + condition * Scalar52_as_Nat L`. When `condition = 1` and the input
lies in `[2^260 - L, 2^260)`, the output is the canonical representative of `self + L`;
when `condition = 0`, the output equals the input.

Source: "curve25519-dalek/src/backend/serial/u64/scalar.rs"
-/

open Aeneas Aeneas.Std Result Aeneas.Std.WP
namespace curve25519_dalek

/- Replace the spec currently in FunsExternal.lean with an alternative phrased in terms of
`Choice.one`/`Choice.zero`.
TODO: make this change throughout the code or revert this. -/

attribute [-step] U64.Insts.SubtleConditionallySelectable.conditional_select_spec
/-- Progress spec for U64.Insts.SubtleConditionallySelectable.conditional_select -/
@[step]
theorem U64.Insts.SubtleConditionallySelectable.conditional_select_spec'
    (a b : U64) (choice : subtle.Choice) :
    U64.Insts.SubtleConditionallySelectable.conditional_select a b choice ⦃ (res : U64) =>
      (choice = Choice.one → res = b) ∧
      (choice = Choice.zero → res = a) ⦄ := by
  unfold U64.Insts.SubtleConditionallySelectable.conditional_select
  cases Choice.eq_zero_or_one choice with
  | inl h => rw [h]; simp [Choice.zero, Choice.one]
  | inr h => rw [h]; simp [Choice.zero, Choice.one]

end curve25519_dalek

open Aeneas Aeneas.Std Result Aeneas.Std.WP
namespace curve25519_dalek.backend.serial.u64.scalar.Scalar52

attribute [-simp] Int.reducePow Nat.reducePow
set_option exponentiation.threshold 260

/-- **Spec theorem**

Specification for
`curve25519_dalek::backend::serial::u64::scalar::Scalar52::conditional_add_l_loop`.
• The function always succeeds (no panic) under the loop invariants on `self`, `mask`, `i`
  and `carry`
• Every output limb is `< 2 ^ 52`
• The value invariant holds:
  `Scalar52_as_Nat result.2 + 2^260 * (result.1 / 2^52) =
   Scalar52_as_Nat self + (if condition = 1 then Scalar52_as_Nat L else 0) +
   2^(52*i) * (carry / 2^52) -
   (if condition = 1 then ∑ j ∈ Ico 0 i, 2^(52*j) * L[j] else 0)` -/
@[step]
theorem conditional_add_l_loop_spec (self : Scalar52) (condition : subtle.Choice)
    (carry : U64) (mask : U64) (i : Usize) (hself : ∀ j < 5, self[j]!.val < 2 ^ 52)
    (hmask : mask.val = 2 ^ 52 - 1) (hi : i.val ≤ 5) (hcarry : carry.val < 2 ^ 53) :
    conditional_add_l_loop self condition carry mask i ⦃ (result : U64 × Scalar52) =>
      (∀ j < 5, result.2[j]!.val < 2 ^ 52) ∧
      (Scalar52_as_Nat result.2 + 2 ^ 260 * (result.1.val / 2 ^ 52) =
        Scalar52_as_Nat self + (if condition = Choice.one then Scalar52_as_Nat constants.L else 0) +
        2 ^ (52 * i.val) * (carry.val / 2 ^ 52) -
        (if condition = Choice.one then ∑ j ∈ Finset.Ico 0 i.val, 2 ^ (52 * j) * constants.L[j]!.val
          else 0)) ⦄ := by
  unfold conditional_add_l_loop
  unfold backend.serial.u64.scalar.Scalar52.Insts.CoreOpsIndexIndexUsizeU64.index
  unfold backend.serial.u64.scalar.Scalar52.Insts.CoreOpsIndexIndexMutUsizeU64.index_mut
  split
  case isTrue hlt =>
    have hi' : i.val < 5 := by agrind
    have hself_i : self[i.val]!.val < 2 ^ 52 := hself i.val hi'
    have hL_i : constants.L[i.val]!.val < 2 ^ 52 := constants.L_limbs_spec i hi'
    step as ⟨i1, hi1⟩  -- L[i]
    step as ⟨addend, haddend_one, haddend_zero⟩  -- conditional_select
    have hi1_eq : i1.val = constants.L[i.val]!.val := by simp [hi1]
    have haddend_bound : addend.val < 2 ^ 52 := by
      cases Choice.eq_zero_or_one condition with
      | inl h => have := haddend_zero h; subst this; decide
      | inr h => have := haddend_one h; subst this; omega
    step as ⟨i2, hi2⟩  -- carry >>> 52
    have hi2_bound : i2.val < 2 := by simp [hi2, Nat.shiftRight_eq_div_pow]; omega
    step as ⟨i3, hi3⟩  -- self[i]
    have hi3_eq : i3.val = self[i.val]!.val := by simp [hi3]
    have hi3_bound : i3.val < 2 ^ 52 := by rw [hi3_eq]; exact hself_i
    have hi2i3_ok : i2.val + i3.val < 2 ^ 64 := by omega
    step as ⟨i4, hi4⟩  -- i2 + i3
    have hi4_bound : i4.val < 2 ^ 52 + 2 := by simp [hi4]; omega
    have hi4addend_ok : i4.val + addend.val < 2 ^ 64 := by omega
    step as ⟨carry1, hcarry1⟩  -- i4 + addend
    have hcarry1_bound : carry1.val < 2 ^ 53 := by simp [hcarry1]; omega
    step as ⟨_, index_mut_back, h_imb, _⟩  -- index_mut
    step as ⟨i5, hi5⟩  -- carry1 &&& mask
    have hi5_mod : i5.val = carry1.val % 2 ^ 52 := by
      simp [hi5, UScalar.val_and, hmask]
    have hi5_bound : i5.val < 2 ^ 52 := by rw [hi5_mod]; exact Nat.mod_lt _ (by omega)
    have hi_plus1_ok : i.val + 1 < 2 ^ 64 := by omega
    step as ⟨i6, hi6⟩  -- i + 1
    have hi6_bound : i6.val ≤ 5 := by simp [hi6]; omega
    have hself1_limbs : ∀ j < 5, (Aeneas.Std.Array.set self i i5)[j]!.val < 2 ^ 52 := by
      intro j hj
      by_cases hjc : j = i.val
      · rw [hjc]
        have := Array.set_of_eq self i5 i (by agrind)
        simp only [UScalar.ofNat_self_val, Array.getElem!_Nat_eq, Array.set_val_eq] at this ⊢
        simpa [this]
      · have := Array.set_of_ne self i5 j i (by agrind) (by agrind) (by omega)
        have := hself j hj
        clear haddend_one haddend_zero
        simp_all only [Array.getElem!_Nat_eq, List.Vector.length_val, UScalar.ofNatCore_val_eq,
          getElem!_pos, UScalarTy.U64_numBits_eq, Bvify.U64.UScalar_bv, UScalar.val_and,
          Nat.and_two_pow_sub_one_eq_mod, Order.add_one_le_iff, UScalar.ofNat_self_val,
          Array.set_val_eq, List.length_set, gt_iff_lt]; agrind
    rw [← h_imb] at hself1_limbs
    step as ⟨res, res_scalar, hres_limbs, hres_inv⟩
    refine ⟨hres_limbs, ?_⟩
    rw [h_imb] at hres_inv
    simp only [hi6] at hres_inv
    have hcarry1_expand : carry1.val = carry.val / 2 ^ 52 + self[i.val]!.val + addend.val := by
      simp [hcarry1, hi4, hi2, Nat.shiftRight_eq_div_pow, hi3_eq]
    have hcarry1_split : i5.val + 2 ^ 52 * (carry1.val / 2 ^ 52) = carry1.val := by
      rw [hi5_mod]; have := Nat.mod_add_div carry1.val (2 ^ 52); omega
    have hpow_split : 2 ^ (52 * (i.val + 1)) = 2 ^ (52 * i.val) * 2 ^ 52 := by
      rw [Nat.mul_add, Nat.mul_one, Nat.pow_add]
    have hself1_nat : Scalar52_as_Nat (Aeneas.Std.Array.set self i i5) =
        Scalar52_as_Nat self - 2 ^ (52 * i.val) * self[i.val]!.val + 2 ^ (52 * i.val) * i5.val := by
      clear haddend_one haddend_zero haddend_bound hres_inv hres_limbs res
      unfold Scalar52_as_Nat
      have h_acc : ∀ j, j < 5 → (Aeneas.Std.Array.set self i i5)[j]!.val =
          if j = i.val then i5.val else self[j]!.val := by
        intro j _
        by_cases h : j = i.val <;> simp only [Array.getElem!_Nat_eq, Array.set_val_eq,
          List.getElem_set, List.length_set, List.Vector.length_val, UScalar.ofNatCore_val_eq,
          getElem!_pos, ↓reduceIte, *]; agrind
      simp only [Finset.sum_range_succ, Finset.range_zero, Finset.sum_empty, zero_add]
      interval_cases i.val <;> simp (config := { decide := true }) only [h_acc 0 (by omega),
        h_acc 1 (by omega), h_acc 2 (by omega), h_acc 3 (by omega), h_acc 4 (by omega),
        ite_true, ite_false] <;> omega
    have : ∑ j ∈ Finset.Ico 0 (i.val + 1), 2 ^ (52 * j) * constants.L[j]!.val =
        ∑ j ∈ Finset.Ico 0 i.val, 2 ^ (52 * j) * constants.L[j]!.val +
        2 ^ (52 * i.val) * constants.L[i.val]!.val := by
      rw [Finset.sum_Ico_succ_top (Nat.zero_le i.val)]
    -- Case split on condition to resolve if-then-else in the value invariant
    cases Choice.eq_zero_or_one condition with
    | inl hc0 =>
      have haddend_val : addend.val = 0 := by
        have := haddend_zero hc0; subst this; rfl
      simp only [hc0, Choice.zero_ne_one, reduceIte, ↓reduceIte] at hres_inv ⊢
      have : 2 ^ (52 * i.val) * i5.val + 2 ^ (52 * i.val) * 2 ^ 52 * (carry1.val / 2 ^ 52) =
          2 ^ (52 * i.val) * carry1.val := by agrind
      have : 2 ^ (52 * i.val) * carry1.val = 2 ^ (52 * i.val) * (carry.val / 2 ^ 52) +
          2 ^ (52 * i.val) * self[i.val]!.val := by
        have : addend.val = 0 := haddend_val; grind [Array.getElem!_Nat_eq]
      rw [hself1_nat, hpow_split] at hres_inv
      have := Scalar52_limb_le_nat self i.val hi'
      omega
    | inr hc1 =>
      have haddend_val : addend.val = constants.L[i.val]!.val := by
        have := haddend_one hc1; subst this; exact hi1_eq
      simp only [hc1, reduceIte] at hres_inv ⊢
      rw [hpow_split] at hres_inv
      have : 2 ^ (52 * i.val) * i5.val + 2 ^ (52 * i.val) * 2 ^ 52 * (carry1.val / 2 ^ 52) =
          2 ^ (52 * i.val) * carry1.val := by agrind
      have : 2 ^ (52 * i.val) * carry1.val =
          2 ^ (52 * i.val) * (carry.val / 2 ^ 52) + 2 ^ (52 * i.val) * self[i.val]!.val +
          2 ^ (52 * i.val) * constants.L[i.val]!.val := by
        have : addend.val = constants.L[i.val]!.val := haddend_val; grind [Array.getElem!_Nat_eq]
      have := Scalar52_limb_le_nat self i.val hi'
      omega
  case isFalse hge =>
    have : i.val = 5 := by agrind
    step*
    refine ⟨by assumption, ?_⟩
    have : ∑ j ∈ Finset.Ico 0 5, 2 ^ (52 * j) * constants.L[j]!.val =
        Scalar52_as_Nat constants.L := by simp [Scalar52_as_Nat]
    cases Choice.eq_zero_or_one condition with
    | inl h => simp [*]
    | inr h =>
      simp only [h, reduceIte] at *
      have hi5 : (↑i : Nat) = 5 := by assumption
      rw [hi5, this]
      omega
termination_by 5 - i.val
decreasing_by agrind

/-- **Spec theorem**

Specification for
`curve25519_dalek::backend::serial::u64::scalar::Scalar52::conditional_add_l`.
• The function always succeeds (no panic) when input limbs are `< 2 ^ 52` and the value
  precondition matches `condition` (see hypotheses)
• Every output limb is `< 2 ^ 52`
• When condition is 1, requires input value in [2^260 - L, 2^260)
• When condition is 1: result + 2^260 = input + L, with result < L and limbs < 2^52
• When condition is 0: result unchanged with limbs < 2^52 -/
@[step]
theorem conditional_add_l_spec (self : Scalar52) (condition : subtle.Choice)
    (hself : ∀ i < 5, self[i]!.val < 2 ^ 52)
    (hself' : condition = Choice.one → 2 ^ 260 ≤ Scalar52_as_Nat self + L)
    (hself'' : condition = Choice.one → Scalar52_as_Nat self < 2 ^ 260)
    (hself''' : condition = Choice.zero → Scalar52_as_Nat self < L) :
    conditional_add_l self condition ⦃ (result : U64 × Scalar52) =>
      (∀ i < 5, result.2[i]!.val < 2 ^ 52) ∧
      (Scalar52_as_Nat result.2 < L) ∧
      (condition = Choice.one → Scalar52_as_Nat result.2 + 2 ^ 260 = Scalar52_as_Nat self + L) ∧
      (condition = Choice.zero → Scalar52_as_Nat result.2 = Scalar52_as_Nat self) ⦄ := by
  unfold conditional_add_l
  step*
  rename_i _ out_scalar
  rw [constants.L_spec] at *
  refine ⟨by assumption, ?_, ?_, ?_⟩
  · -- result < L
    cases Choice.val_eq_zero_or_one condition with
    | inl =>
      have := Choice.eq_zero_of_val condition (by assumption)
      have : Scalar52_as_Nat out_scalar + 2 ^ 260 * (result.val / 2 ^ 52) =
          Scalar52_as_Nat self := by simp [*]
      agrind
    | inr =>
      have := Choice.eq_one_of_val condition (by assumption)
      have : Scalar52_as_Nat out_scalar < 2 ^ 260 :=
        Scalar52_as_Nat_bounded out_scalar (by assumption)
      grind [Finset.Ico_self]
  · -- condition = Choice.one case
    have : Scalar52_as_Nat out_scalar < 2 ^ 260 :=
      Scalar52_as_Nat_bounded out_scalar (by assumption)
    grind [Finset.Ico_self, L_lt]
  · -- condition = Choice.zero case
    intro _
    have : Scalar52_as_Nat out_scalar + 2 ^ 260 * (result.val / 2 ^ 52) = Scalar52_as_Nat self := by
      simp [*]
    agrind [L_lt]

end curve25519_dalek.backend.serial.u64.scalar.Scalar52
