/-
Copyright 2025 The Beneficial AI Foundation. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Butterley
-/
import Aeneas
import Curve25519Dalek.Funs
import Curve25519Dalek.Aux
import Curve25519Dalek.Math.Basic
import Curve25519Dalek.Specs.Backend.Serial.U64.Scalar.Scalar52.ConditionalAddL
import Curve25519Dalek.Specs.Backend.Serial.U64.Scalar.Scalar52.Zero

/-! # Spec theorem for `curve25519_dalek::backend::serial::u64::scalar::Scalar52::sub`

This function computes the difference of two Scalar52 values modulo L (the group order).
The function performs subtraction with borrow handling and conditional addition of L
to ensure the result is non-negative.

The subtraction uses base-2^52 arithmetic with borrow propagation:

1. **Loop iteration**: For each limb i:
   - `borrow = a[i].wrapping_sub(b[i] + (borrow >> 63))`
   - `difference[i] = borrow & mask` (keep lower 52 bits)

2. **Borrow detection**: `borrow >> 63` extracts a 0/1 flag:
   - 0 if no underflow occurred
   - 1 if underflow occurred (wrapped value has top bits set)

3. **Telescoping property**: The borrows cancel perfectly:
   - `difference[i] = (a[i] - b[i] - c_i) mod 2^52 = a[i] - b[i] - c_i + c_{i+1} * 2^52`
   - Summing: `Σ difference[i] * 2^(52*i) = A - B + c_5 * 2^260`

4. **Final correction**: If `c_5 = 1` (final borrow set), then `A < B`, so add L
   to get a positive result in `[0, L)`.

**Key insight**: The final borrow `c_5` is just a sign indicator, not a quantity to subtract.
When `A < B`, the difference array stores `2^260 + (A - B)` (the representation in Z/(2^260)Z).
Adding L causes wrap-around: `(2^260 + (A - B) + L) mod 2^260 = A - B + L ∈ (0, L)`.

Source: curve25519-dalek/src/backend/serial/u64/scalar.rs:L175-L198
-/

open Aeneas Aeneas.Std Result Aeneas.Std.WP
namespace curve25519_dalek.backend.serial.u64.scalar.Scalar52

set_option exponentiation.threshold 260
attribute [-simp] Int.reducePow Nat.reducePow

/-! ## Spec for `sub` -/

/-- Partial sum of limbs up to index n: Σ (j < n) limbs[j] * 2^(52*j) -/
def Scalar52_partial_as_Nat (limbs : Array U64 5#usize) (n : Nat) : Nat :=
  ∑ j ∈ Finset.range n, 2 ^ (52 * j) * (limbs[j]!).val

set_option maxHeartbeats 1200000 in -- proof could be better
/-- **Spec theorem**

Specification for
`curve25519_dalek::backend::serial::u64::scalar::Scalar52::sub_loop`.

The loop computes the subtraction `a - b` with borrow propagation. After processing
indices `0..i`, the loop invariant
  `partial_a(i) + (borrow / 2^63) * 2^(52*i) = partial_b(i) + partial_diff(i)`
holds; on completion (`i = 5`) this becomes
  `Scalar52_as_Nat a + (borrow / 2^63) * 2^260 = Scalar52_as_Nat b + Scalar52_as_Nat result.1`,
where `borrow / 2^63 = 1` flags an underflow (`a < b`).
• Every output limb of `result.1` is `< 2 ^ 52`
• The closing value invariant above holds
-/
@[step]
theorem sub_loop_spec (a b difference : Scalar52) (mask borrow : U64) (i : Usize)
    (ha : ∀ j < 5, a[j]!.val < 2 ^ 52)
    (hb : ∀ j < 5, b[j]!.val < 2 ^ 52)
    (hdiff : ∀ j < i.val, difference[j]!.val < 2 ^ 52)
    (hdiff_rest : ∀ j, i.val ≤ j → j < 5 → difference[j]!.val = 0)
    (hmask : mask.val = 2 ^ 52 - 1)
    (hi : i.val ≤ 5)
    (hborrow : borrow.val / 2 ^ 63 ≤ 1)
    (hinv : Scalar52_partial_as_Nat a i.val + borrow.val / 2 ^ 63 * 2 ^ (52 * i.val) =
            Scalar52_partial_as_Nat b i.val + Scalar52_partial_as_Nat difference i.val) :
    sub_loop a b difference mask borrow i ⦃ (result : Scalar52 × U64) =>
      (∀ j < 5, result.1[j]!.val < 2 ^ 52) ∧
      (Scalar52_as_Nat a + result.2.val / 2 ^ 63 * 2 ^ 260 =
        Scalar52_as_Nat b + Scalar52_as_Nat result.1) ⦄ := by
  unfold sub_loop
  unfold backend.serial.u64.scalar.Scalar52.Insts.CoreOpsIndexIndexUsizeU64.index
  unfold backend.serial.u64.scalar.Scalar52.Insts.CoreOpsIndexIndexMutUsizeU64.index_mut
  split
  case isTrue hlt =>
    have hi' : i.val < 5 := by agrind
    have ha_i : a[i.val]!.val < 2 ^ 52 := ha i.val hi'
    have hb_i : b[i.val]!.val < 2 ^ 52 := hb i.val hi'
    have hborrow_bit : borrow.val >>> 63 ≤ 1 := by
      have hbv : borrow.val < 2 ^ 64 := by agrind
      omega
    -- Step through the operations manually
    step as ⟨i1, hi1⟩  -- a[i]
    step as ⟨i2, hi2⟩  -- b[i]
    step as ⟨i3, hi3_1, hi3_2⟩  -- borrow >>> 63
    have hi3_bound : i3.val ≤ 1 := by grind
    have hi2_bound : i2.val < 2 ^ 52 := by simp only [hi2]; simp_all
    have hi4_ok : i2.val + i3.val < 2 ^ 64 := by omega
    step as ⟨i4, hi4⟩  -- i2 + i3
    step as ⟨borrow1, hborrow1⟩  -- wrapping_sub
    step as ⟨_, index_mut_back, h_imb, _⟩  -- index_mut
    step as ⟨i5, hi5_1, hi5_2⟩  -- borrow1 &&& mask
    step as ⟨i6, hi6⟩  -- i + 1
    -- Set up the recursive call hypotheses
    have hborrow1_bound : borrow1.val / 2 ^ 63 ≤ 1 := by
      have hb1 : borrow1.val < 2 ^ 64 := by agrind
      omega
    -- i5 = borrow1 &&& mask = borrow1 % 2^52
    have hi5_mod : i5.val = borrow1.val % 2 ^ 52 := by
      have := Nat.and_two_pow_sub_one_eq_mod borrow1.val 52
      grind
    have hi5_bound : i5.val < 2 ^ 52 := by
      rw [hi5_mod]
      exact Nat.mod_lt borrow1.val (by decide : 2 ^ 52 > 0)
    have hdiff1 : ∀ j < i6.val, (Aeneas.Std.Array.set difference i i5)[j]!.val < 2 ^ 52 := by
      intro j hj
      simp only [hi6] at hj
      by_cases hjc : j = i.val
      · grind
      · have hj' : j < i.val := by omega
        have hne := Array.set_of_ne difference i5 j i (by agrind) (by agrind) (by omega)
        simp only [Array.getElem!_Nat_eq, Array.set_val_eq] at hne ⊢
        have hdiff_j := hdiff j hj'
        simp only [Array.getElem!_Nat_eq] at hdiff_j
        simp_all
    have hdiff1_rest : ∀ j, i6.val ≤ j → j < 5 → (Aeneas.Std.Array.set difference i i5)[j]!.val = 0
      := by
      intro j hji hj5
      simp only [hi6] at hji
      have hne : j ≠ i.val := by omega
      have := Array.set_of_ne' difference i5 j i (by agrind) (by omega)
      simp only [Array.getElem!_Nat_eq, Array.set_val_eq] at this ⊢
      have hr := hdiff_rest j (by omega) hj5
      simp only [Array.getElem!_Nat_eq] at hr
      simp_all
    -- Main proof: the loop invariant is preserved
    have hinv1 : Scalar52_partial_as_Nat a i6.val + borrow1.val / 2 ^ 63 * 2 ^ (52 * i6.val) =
                 Scalar52_partial_as_Nat b i6.val +
                 Scalar52_partial_as_Nat (Aeneas.Std.Array.set difference i i5) i6.val := by
      have hws : borrow1.val = (i1.val + (2^64 - i4.val)) % 2^64 := by
        simp only [hborrow1]
        have := U64.wrapping_sub_val_eq i1 i4
        simp only [UScalar.size] at this
        exact this
      have hi1_val : i1.val = a[i.val]!.val := by
        simp only [hi1, UScalar.val, Array.getElem!_Nat_eq]
      have hi2_val : i2.val = b[i.val]!.val := by
        simp only [hi2, UScalar.val, Array.getElem!_Nat_eq]
      have hi4_val : i4.val = b[i.val]!.val + i3.val := by
        simp only [hi4, hi2_val]
      have hi3_eq : i3.val = borrow.val / 2^63 := by
        simp only [hi3_1, Nat.shiftRight_eq_div_pow]
      have hi6_eq : i6.val = i.val + 1 := by simp [hi6]
      simp only [hi6_eq, Scalar52_partial_as_Nat]
      simp only [Finset.range_add_one, Finset.sum_insert (Finset.notMem_range_self)]
      have hdiff'_lt : ∀ j < i.val,
          (Aeneas.Std.Array.set difference i i5)[j]!.val = difference[j]!.val := by
        intro j hj
        have h := Array.set_of_ne difference i5 j i (by agrind) (by agrind) (by omega)
        grind [Array.getElem!_Nat_eq, Array.set_val_eq, UScalar.val]
      have hdiff'_eq : (Aeneas.Std.Array.set difference i i5)[i.val]!.val = i5.val := by
        have h := Array.set_of_eq difference i5 i (by agrind)
        grind [Array.getElem!_Nat_eq, Array.set_val_eq, UScalar.val]
      have hdiff'_partial : ∑ j ∈ Finset.range i.val, 2^(52*j) *
                          (Aeneas.Std.Array.set difference i i5)[j]!.val
                          = ∑ j ∈ Finset.range i.val, 2^(52*j) * difference[j]!.val := by
        apply Finset.sum_congr rfl
        intro j hj
        simp only [Finset.mem_range] at hj
        rw [hdiff'_lt j hj]
      rw [hdiff'_partial, hdiff'_eq]
      have hinv' := hinv
      simp only [Scalar52_partial_as_Nat] at hinv'
      have hold_borrow : borrow.val / 2^63 = i3.val := by omega
      have ha_bound : i1.val < 2^52 := by rw [hi1_val]; exact ha_i
      have hb_i_bound : b[i.val]!.val < 2^52 := hb_i
      have hi3_le : i3.val ≤ 1 := hi3_bound
      have hi4_bound : i4.val < 2^52 + 1 := by rw [hi4_val]; omega
      set P := 2^(52*i.val) with hP_def
      by_cases hcase : i1.val ≥ i4.val
      · -- Case 1: No underflow
        have hborrow1_val : borrow1.val = i1.val - i4.val := by
          rw [hws]
          have h1 : i1.val + (2^64 - i4.val) = 2^64 + (i1.val - i4.val) := by omega
          rw [h1]
          have h2 : i1.val - i4.val < 2^64 := by omega
          omega
        have hborrow1_lt : borrow1.val < 2^63 := by rw [hborrow1_val]; omega
        have hnew_borrow : borrow1.val / 2^63 = 0 := by omega
        have hi5_val : i5.val = borrow1.val := by
          rw [hi5_mod, hborrow1_val]
          have h : i1.val - i4.val < 2^52 := by omega
          exact Nat.mod_eq_of_lt h
        simp only [hnew_borrow, zero_mul, add_zero]
        rw [hi5_val, hborrow1_val]
        have hlimb : a[i.val]!.val = b[i.val]!.val + i3.val + (i1.val - i4.val) := by
          rw [← hi1_val, hi4_val]; omega
        -- New aeneas elaborator no longer auto-applies `Array.getElem!_Nat_eq`,
        -- so we normalise the indexing form explicitly before `grind`.
        simp only [Array.getElem!_Nat_eq] at *
        grind
      · -- Case 2: Underflow occurred
        have hi1_lt_i4 : i1.val < i4.val := by omega
        have hborrow1_val : borrow1.val = 2^64 + i1.val - i4.val := by rw [hws]; omega
        have hborrow1_ge : borrow1.val ≥ 2^63 := by rw [hborrow1_val]; omega
        have hborrow1_lt_264 : borrow1.val < 2^64 := by rw [hborrow1_val]; omega
        have hnew_borrow : borrow1.val / 2^63 = 1 := by omega
        have hi5_val : i5.val = 2^52 + i1.val - i4.val := by
          rw [hi5_mod, hborrow1_val]
          have hdiff_val : i4.val - i1.val ≤ 2^52 := by omega
          have hdpos : 0 < i4.val - i1.val := by omega
          have heq1 : 2^64 + i1.val - i4.val = 2^64 - (i4.val - i1.val) := by omega
          rw [heq1]
          have heq2 : 2^64 - (i4.val - i1.val) = (2^12 - 1) * 2^52 +
            (2^52 - (i4.val - i1.val)) := by
              have h : (2:ℕ)^64 = (2:ℕ)^12 * (2:ℕ)^52 := by decide
              omega
          rw [heq2]
          have heq3 : (2^12 - 1) * 2^52 + (2^52 - (i4.val - i1.val)) =
                      (2^52 - (i4.val - i1.val)) + (2^12 - 1) * 2^52 := by ring
          rw [heq3, Nat.add_mul_mod_self_right]
          have hlt : 2^52 - (i4.val - i1.val) < 2^52 := by omega
          rw [Nat.mod_eq_of_lt hlt]
          omega
        simp only [hnew_borrow, one_mul]
        rw [hi5_val]
        have hpow_succ : 2^(52*(i.val + 1)) = 2^52 * P := by
          simp only [hP_def]
          have : 52 * (i.val + 1) = 52 + 52 * i.val := by ring
          rw [this, Nat.pow_add]
        rw [hpow_succ]
        have hlimb : a[i.val]!.val + 2^52 = b[i.val]!.val + i3.val + (2^52 + i1.val - i4.val) := by
          rw [← hi1_val, hi4_val]; omega
        -- New aeneas elaborator no longer auto-applies `Array.getElem!_Nat_eq`,
        -- so we normalise the indexing form explicitly before `grind`.
        simp only [Array.getElem!_Nat_eq] at *
        grind
    -- Rewrite hypotheses from Array.set form to index_mut_back form
    rw [← h_imb] at hdiff1 hdiff1_rest hinv1
    -- New aeneas `do` elaborator (PR #963) uncurries the (Scalar52 × U64) result,
    -- so we now have one binder per tuple component followed by the two conjuncts.
    step as ⟨res_arr, res_carry, hres1, hres2⟩
    refine ⟨hres1, ?_⟩
    exact hres2
  case isFalse hge =>
    refine ⟨by grind [Array.getElem!_Nat_eq], ?_⟩
    unfold Scalar52_partial_as_Nat Scalar52_as_Nat at *
    -- New aeneas elaborator no longer auto-applies `Array.getElem!_Nat_eq`.
    simp only [Array.getElem!_Nat_eq] at *
    grind
termination_by 5 - i.val
decreasing_by scalar_decr_tac

/-- **Spec theorem for `curve25519_dalek::backend::serial::u64::scalar::Scalar52::sub`**
• The function always succeeds (no panic) when both inputs have limbs `< 2 ^ 52` and the
  value preconditions hold (`a < b + L` and `b ≤ L`)
• `Scalar52_as_Nat result + Scalar52_as_Nat b ≡ Scalar52_as_Nat a (mod L)`
• `Scalar52_as_Nat result < L`, the canonical reduced representative
• Every output limb is `< 2 ^ 52` -/
@[step]
theorem sub_spec (a b : Array U64 5#usize)
    (ha : ∀ i < 5, a[i]!.val < 2 ^ 52)
    (hb : ∀ i < 5, b[i]!.val < 2 ^ 52)
    (ha' : Scalar52_as_Nat a < Scalar52_as_Nat b + L)
    (hb' : Scalar52_as_Nat b ≤ L) :
    sub a b ⦃ (result : Scalar52) =>
      Scalar52_as_Nat result + Scalar52_as_Nat b ≡ Scalar52_as_Nat a [MOD L] ∧
      Scalar52_as_Nat result < L ∧
      (∀ i < 5, result[i]!.val < 2 ^ 52) ⦄ := by
  unfold sub
  unfold subtle.Choice.Insts.CoreConvertFromU8.from
  -- First, step through mask computation (two steps: shift then subtract)
  step  -- 1 <<< 52
  step  -- mask = i - 1
  -- Progress through the loop with all required hypotheses.
  -- New aeneas `do` elaborator (PR #963) uncurries tuple-returning steps:
  -- sub_loop returns `Scalar52 × U64`, so we destructure it as two binders.
  step as ⟨diff, borrow, hdiff_limbs, hdiff_inv⟩
  · -- hdiff_rest: ZERO[j] = 0 for all j in 0..5
    intro j _ hj5
    interval_cases j <;> simp only [ZERO] <;> decide
  · -- hinv: Initial invariant at i=0 - partial sums are all 0
    have h0 : (0#usize).bv.toNat = 0 := by decide
    have h1 : (0#u64).bv.toNat = 0 := by decide
    simp only [Scalar52_partial_as_Nat, UScalar.val, h0, h1,
               Finset.range_zero, Finset.sum_empty, zero_add, Nat.zero_div, zero_mul]
  -- diff_inv: A + (borrow/2^63) * 2^260 = B + D
  -- Progress through borrow >>> 63 and cast
  step as ⟨i1, hi1_eq, _⟩  -- borrow >>> 63
  have : i1.val ≤ 1 := by simp only [*, Nat.shiftRight_eq_div_pow]; grind
  step as ⟨i2, hi2⟩  -- cast to U8
  have hi2_eq_i1 : i2.val = i1.val := by simp only [UScalar.val]; grind
  -- Helper: Choice.one.val = 1
  have : Choice.one.val.val = 1 := by rfl
  -- Helper: Choice.zero.val = 0
  have : Choice.zero.val.val = 0 := by rfl
  -- Now split on whether i2 = 0 or i2 = 1 (for CoreConvertFromU8.from)
  split
  next hi2_zero =>
    -- i2 = 0, so no borrow, A >= B
    have : i2.val = 0 := by simp only [hi2_zero]; rfl
    have hborrow_div : borrow.val / 2 ^ 63 = 0 := by simp_all [Nat.shiftRight_eq_div_pow]
    have hdiff_val : Scalar52_as_Nat diff = Scalar52_as_Nat a - Scalar52_as_Nat b := by grind
    have : Scalar52_as_Nat a ≥ Scalar52_as_Nat b := by grind
    -- Progress through conditional_add_l with Choice.zero.
    -- New aeneas `do` elaborator (PR #963) uncurries the (U64 × Scalar52) result,
    -- so we now have one binder per tuple component followed by the four conjuncts.
    step as ⟨_cond_carry, cond_arr, hres_limbs, hres_lt_L, _, hres_eq_zero⟩
    have hcz : (subtle.Choice.mk i2 (Or.inl hi2_zero)) = Choice.zero := by
      simp only [Choice.zero, hi2_zero]
    have hres_val := hres_eq_zero hcz
    refine ⟨?_, by grind, by grind [Array.getElem!_Nat_eq]⟩
    -- Modular equivalence: res + B ≡ A [MOD L]
    rw [hres_val, hdiff_val]
    have : Scalar52_as_Nat a - Scalar52_as_Nat b + Scalar52_as_Nat b = Scalar52_as_Nat a := by omega
    rw [this]
  next hi2_nonzero =>
    split
    next hi2_one =>
      -- i2 = 1, so borrow occurred, A < B
      have : borrow.val / 2 ^ 63 = 1 := by
        simp only [*, Nat.shiftRight_eq_div_pow] at *
        grind
      have : Scalar52_as_Nat diff < 2 ^ 260 := Scalar52_as_Nat_bounded diff hdiff_limbs
      -- Progress through conditional_add_l with Choice.one.
      -- New aeneas `do` elaborator (PR #963) uncurries the (U64 × Scalar52) result,
      -- so we now have one binder per tuple component followed by the four conjuncts.
      step as ⟨_cond_carry, cond_arr, hres_limbs, hres_lt_L, hres_eq_one, _⟩
      refine ⟨?_, by grind, by grind [Array.getElem!_Nat_eq]⟩
      · -- Modular equivalence: res + B = A + L ≡ A [MOD L]
        have hL_mod : L ≡ 0 [MOD L] := by rw [Nat.ModEq, Nat.zero_mod, Nat.mod_self]
        have : Scalar52_as_Nat a + L ≡ Scalar52_as_Nat a + 0 [MOD L] := Nat.ModEq.add_left _ hL_mod
        have : (subtle.Choice.mk i2 (Or.inr hi2_one)) = Choice.one := by simp [Choice.one, hi2_one]
        grind
    next hi2_neither =>
      grind

end curve25519_dalek.backend.serial.u64.scalar.Scalar52
